WHITESPACE = _{ " " | "\t" | "\r\n" | "\n" }

path_formula = { ("Forall" | "Exists") ~ ident ~ "." ~ form_rec }
form_rec = { path_formula | traj_formula | inner_hltl }
traj_formula = { ("A" | "E") ~ ident ~ "." ~ ahltl_form_rec }
ahltl_form_rec = { traj_formula | inner_altl }

inner_hltl = { hequal } 
inner_altl = { aequal }

hequal = { himpl ~ ("=" ~ hequal)? }
himpl = { hdisj ~ ("->" ~ himpl)? }
hdisj = { hconj ~ ("|" ~ hdisj)? }
hconj = { hfactor ~ ("&" ~ hconj)? }
hfactor = { unop ~ hfactor | "(" ~ inner_hltl ~ ")" | hltl_atom }
hltl_atom = { ident ~ "[" ~ ident ~ "]" }

aequal = { aimpl ~ ("=" ~ aequal)? }
aimpl = { adisj ~ ("->" ~ aimpl)? }
adisj = { aconj ~ ("|" ~ adisj)? }
aconj = { afactor ~ ("&" ~ aconj)? }
afactor = { aunop ~ afactor | "(" ~ inner_altl ~ ")" | altl_atom }
altl_atom = { ident ~ "[" ~ ident ~ "]" ~ "[" ~ ident ~ "]" }

aunop = { "G" | "F" | "~" }
unop = { "G" | "F" | "X" | "~" }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

// Entry point for the parser
formula = _{ SOI ~ path_formula ~ EOI }